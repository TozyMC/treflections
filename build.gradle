import java.time.Year

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'xyz.tozymc.spigot'
version '1.0'
description = 'Reflection libraries for working with multi Minecraft version.'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
}

dependencies {
    api 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    api 'org.jetbrains:annotations:21.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

compileJava {
    options.encoding = 'UTF-8'
}

javadoc {
    source = sourceSets.main.allJava
    if (project.hasProperty("javadocExecutable")) {
        executable = javadocExecutable
    }
    options {
        charSet 'utf-8'
        source '1.8'
        bottom "Copyright &#169; ${Year.now()} TozyMC. All rights reserved."
        links << 'https://docs.oracle.com/javase/8/docs/api/'
        links << 'https://hub.spigotmc.org/javadocs/spigot/'
        links << 'https://javadoc.io/doc/org.jetbrains/annotations/21.0.1/'
    }
    exclude 'xyz/tozymc/util/**'
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            convertToProvidedScope(pom)
            pom {
                name = rootProject.name
                description = project.description
                url = 'https://github.com/TozyMC/treflections'
                inceptionYear = '2021'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/TozyMC/treflections/blob/main/LICENSE'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/TozyMC/treflections.git'
                    developerConnection = 'scm:git:ssh://github.com:TozyMC/treflections.git'
                    url = 'https://github.com/TozyMC/treflections'
                }

                developers {
                    developer {
                        id = 'tozymc'
                        name = 'TozyMC'
                        url = 'https://tozymc.xyz'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

def convertToProvidedScope(pom) {
    pom.withXml {
        asNode().dependencies.'*'.findAll() {
            configurations.api.allDependencies.find { dep ->
                dep.name == it.artifactId.text()
            }
        }.each() {
            it.scope*.value = 'provided'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}